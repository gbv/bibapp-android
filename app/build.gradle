plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

def secretsPropertiesFile = rootProject.file("secrets.properties")
def secretProperties = new Properties()

if (secretsPropertiesFile.exists()) {
    secretProperties.load(new FileInputStream(secretsPropertiesFile))
} else {
    secretProperties.setProperty("store_file", "${System.getenv('store_file')}")
    secretProperties.setProperty("signing_keystore_password", "${System.getenv('signing_keystore_password')}")
    secretProperties.setProperty("signing_key_password", "${System.getenv('signing_key_password')}")
    secretProperties.setProperty("signing_key_alias", "${System.getenv('signing_key_alias')}")
}

android {
    compileSdk 30

    defaultConfig {
        minSdk 21
        targetSdk 30
        applicationId "de.eww.bibapp"

        versionCode buildVersionCode()
        versionName version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding true
    }

    lintOptions {
        checkDependencies true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
    }

    flavorDimensions "version"

    productFlavors {
        hi {
            dimension "version"
            applicationIdSuffix ".hi"
        }
        il {
            dimension "version"
            applicationIdSuffix ".il"
        }
        bls {
            dimension "version"
            applicationIdSuffix ".bls"
        }
        hawk {
            dimension "version"
            applicationIdSuffix ".hawk"
        }
        thulb {
            dimension "version"
            applicationIdSuffix ".thulb"
        }
        eahjena {
            dimension "version"
            applicationIdSuffix ".eahjena"
        }
        bathueringen {
            dimension "version"
            applicationIdSuffix ".bathueringen"
        }
        schmalkalden {
            dimension "version"
            applicationIdSuffix ".schmalkalden"
        }
        ubweimar {
            dimension "version"
            applicationIdSuffix ".ubweimar"
        }
        hfmweimar {
            dimension "version"
            applicationIdSuffix ".hfmweimar"
        }
        ndh {
            dimension "version"
            applicationIdSuffix ".ndh"
        }
        ufb {
            dimension "version"
            applicationIdSuffix ".ufb"
        }
        tubs {
            dimension "version"
            applicationIdSuffix '.tubs'
        }
        aezb {
            dimension "version"
            applicationIdSuffix '.aezb'
        }
        commerz {
            dimension "version"
            applicationIdSuffix '.commerz'
        }
    }

    signingConfigs {
        release {
            storeFile file(secretProperties['store_file'])
            storePassword secretProperties['signing_keystore_password']
            keyPassword secretProperties['signing_key_password']
            keyAlias secretProperties['signing_key_alias']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    // AndroidX / Support
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.navigation:navigation-fragment:2.3.5"
    implementation "androidx.navigation:navigation-ui:2.3.5"
    implementation "androidx.preference:preference:1.1.1"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.paging:paging-runtime:3.0.1"
    implementation "androidx.paging:paging-rxjava2:3.0.1"
    //implementation "androidx.paging:paging-compose:1.0.0-alpha13"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0-rc01"

    // Testing
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Retrofit / OkHttp
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.retrofit2:converter-scalars:2.9.0"
    implementation ("com.squareup.retrofit2:converter-simplexml:2.9.0") {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.9.0"
    //implementation "com.squareup.okhttp3:okhttp-urlconnection:3.0.1"
    implementation "com.squareup.okhttp3:logging-interceptor:3.14.9"

    // Google Play services
    implementation 'com.google.android.gms:play-services-maps:17.0.1'
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'com.google.maps.android:android-maps-utils:0.5'

    // Iconics
    implementation 'com.mikepenz:iconics-core:3.2.5'
    implementation 'com.mikepenz:iconics-views:3.2.5'

    // ZXing
    implementation 'com.google.zxing:core:3.3.3'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
}